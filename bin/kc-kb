#!/usr/bin/env perl
use strict; use warnings;

# NOTES:
#  how do we send 'super' key (alone or as key combo)
#  ctrl + key not seen by termux (but e.g ctl+a is not sent, while ctl+j sends 'j')
#  "Return" (12) doesn't enter in termux (ctrl-J works, but ctl must be toggled on screen)
#  alt-tab eaten by os. not set

# control characters from
# https://github.com/KDE/kdeconnect-kde/blob/master/plugins/remotekeyboard/remotekeyboardplugin.cpp
my %cntr=(
   'BackSpace'=> 1,
   'Tab'=> 2,
   'Left'=> 4,
   'Up'=> 5,
   'Right'=> 6,
   'Down'=> 7,
   'Prior'=> 8, 'PageUp'=> 8,
   'Next'=> 9, 'PageDown'=> 9,
   'Home'=> 10,
   'End'=> 11,
   'Return'=> 12, 'Enter'=> 12,
   'Delete'=> 13,
   'Escape'=> 14,
   'SysReq'=> 15,
   'ScrollLock'=> 16,
    # F1-F12 == 21 - 32 
);

# xev named symbols to actual key
my %syms=(
  'space' => ' ',
  'exclam'=> '!',
  'at'=> '@',
  'numbersign'=> '#',
  'dollar'=> '$',
  'percent'=> '%',
  'asciicircum'=> '^',
  'ampersand'=> '&',
  'asterisk'=> '*',
  'parenleft'=> '(',
  'parenright'=> ')',
  'minus'=> '-',
  'underscore'=> '_',
  'equal'=> '=',
  'plus'=> '+',
  'slash'=> '/',
  'less'=> '<',
  'greater'=> '>',
  'comma'=> ',',
  'period'=> '.',
  'bracketleft'=> '[',
  'bracketright'=> ']',
  'braceleft' => '{',
  'braceright' => '}',
  'backslash'=> '\\',
  'bar'=> '|');

my $devid;
$devid = $1 if qx/kdeconnect-cli -l/ =~ m/: (\w+) \(paired and reachable/ ;
die 'no connected device? see `kdeconnect-cli -l`' if not $devid;
my $cmd="qdbus org.kde.kdeconnect /modules/kdeconnect/devices/$devid/remotekeyboard sendKeyPress";
print "using '$devid' like:\n\t$cmd\n";

open my $p, "xev|" or die "cannot open xev pipe: $!";

my $event="";
my %mods=('Control'=>0,'Alt'=>0, 'Shift'=>0);
# parse output of kev
while($_=<$p>){
  if(/^Key(Release|Press)/) {
    $event=$1;
  } elsif(/ (Control|Alt|Shift)/) {
    # turn on or off mods
    $mods{$1} = $event eq "Press"?1:0;
    print "$1 $event => $mods{$1}!\n";
  } elsif($event =~ /Release/ and /keysym .x[\da-f]+, (\w+)\W/){
    my $k=$syms{$1}||$1;
    my $c=$cntr{$k}||0;
    # unhandled speicall key
    if($c==0 && length($k)>1){
	print "$k: unspecified symbol not sent\n";
	next;
    }
    # send key
    qx:$cmd '$k' $c $mods{Shift} $mods{Control} $mods{Alt}:;
    # DEBUG
    print "$cmd '$k' $c $mods{Shift} $mods{Control} $mods{Alt}\n";
  }
}
